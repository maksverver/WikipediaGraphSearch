pkgname=wikipath
pkgver=0.2
pkgrel=1
pkgdesc="Wikipedia shortest path search tool"
arch=('x86_64')
url="https://github.com/maksverver/WikipediaGraphSearch"
depends=('sqlite' 'libxml2')
optdepends=(
    'mailcap: provides /etc/mime.types used by the Python HTTP server'
    'python: Python bindings and Python HTTP server'
    'wt: Wt HTTP server'
)
# boost: needed by wt to build the (deprecated) websearch binary
# mailcap: needed for python_http_server_test.py
# pybind11: needed for the Python bindings
makedepends=('boost' 'cmake' 'mailcap' 'python' 'pybind11' 'wt')

_build_dir=${startdir}/build-package

prepare() {
    ln -nsf "${startdir}"/.. "${pkgname}-${pkgver}"
}

# https://wiki.archlinux.org/title/CMake_package_guidelines
build() {
    cmake -B build -S "$pkgname-$pkgver" \
            -D CMAKE_BUILD_TYPE=None \
            -D CMAKE_INSTALL_PREFIX=/usr
    cmake --build build
}

check() {
    ctest --test-dir build --output-on-failure
}

package() {
    DESTDIR=${pkgdir} cmake --install build

    # Directory for user-installed Wikipedia graphs:
    install -d "${pkgdir}/var/lib/wikipath"
}
